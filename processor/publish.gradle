apply plugin: "maven-publish"
//apply plugin: "signing"

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
            maven {
                name github.name()
                url github.url()
                credentials {
                    username github.username()
                    password github.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = releaseConfig.group
                version = releaseConfig.version

                artifactId = 'processor'

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'JsonApiX Processor'
                    description = 'JsonApiX Processor module'
                    url = 'https://github.com/infinum/android-jsonapix'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'stef'
                            name = 'Stjepan Banek'
                            email = 'stjepan.banek@infinum.com'
                        }
                        developer {
                            id = 'gob'
                            name = 'Nikola Gobovic'
                            email = 'nikola.gobovic@infinum.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/android-jsonapix.git'
                        developerConnection = 'https://github.com/infinum/android-jsonapix.git'
                        url = 'https://github.com/infinum/android-jsonapix'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}